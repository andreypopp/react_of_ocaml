open ContainersLabels
include Hx_runtime

let hx_runtime () =
  let static =
    Hx_static.Sites.static
    |> List.head_opt
    |> Option.get_exn_or "no /static dir found"
  in
  Dream.get "/static/**" (Dream.static static)

let hx_page path ui =
  let scripts = [ "/static/hx_runtime.js" ] in
  Dream.get path (React_dream.render ~scripts ui)

let hx_handle (verb : Dream.method_) path ui =
  let handle = React_dream.render ui in
  let handle =
    match verb with
    | `Method verb -> (
        fun req ->
          match Dream.method_ req with
          | `Method verb' when String.equal verb' verb -> handle req
          | _ ->
              Dream.respond ~status:`Method_Not_Allowed
                "METHOD NOT ALLOWED")
    | _ -> handle
  in
  let route =
    match verb with
    | `GET -> Dream.get
    | `POST -> Dream.post
    | `PUT -> Dream.put
    | `DELETE -> Dream.delete
    | `PATCH -> Dream.patch
    | `HEAD -> Dream.head
    | `TRACE -> Dream.trace
    | `OPTIONS -> Dream.options
    | `CONNECT -> Dream.connect
    | `Method _ -> Dream.any
  in
  route path handle
