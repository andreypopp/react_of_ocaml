[@@@warning "-32-27-26"]

let%component link' ~href ~label () =
  let%browser_only onClick ev =
    ReactEvent.Mouse.preventDefault ev;
    React_browser.Router.navigate href
  in
  <a href onClick>(React.string label)</a>

let%component with_children ~children () = <div />

let%component button' ~onPress:onClick ~label () =
  <button className="pv1 ph2 br1 bg-light-gray bw1 b--gray" onClick>
    (React.string label) <with_children>"OK"</with_children>
  </button>

let%component hello ~name () =
  let q, setq = React.useState (fun () -> Api.Hello.hello ~name) in
  let () =
    React.useEffect1
      (fun () ->
        React.startTransition (fun () ->
        setq (fun _ -> Api.Hello.hello ~name));
        None)
      [| name |]
  in
  let msg = React.use (Remote.run_query q) in
  let%browser_only onClick _ev =
    ignore
      (Promise.(
         let* () =
           Remote.run_mutation
           @@ Api.Hello.update_greeting ~greeting:Api.Greeting_informal
         in
         Remote.invalidate (Api.Hello.hello ~name);
         React.startTransition (fun () ->
         setq (fun _ -> Api.Hello.hello ~name));
         return ())
        : unit Promise.t)
  in
  <div onClick>(React.string msg)</div>

let%component counter ~init ~title () =
  let v, setv = React.useState (Fun.const init) in
  let succ _ev =
    React.startTransition @@ fun () -> setv Int.succ
  in
  let pred _ev =
    React.startTransition @@ fun () -> setv Int.pred
  in
  let reset _ev =
    React.startTransition @@ fun () -> setv (Fun.const 0)
  in
  <div className="pa4">
    <h2>(React.string title)</h2>
    <p>(React.string ("clicked " ^ string_of_int v ^ " times"))</p>
    <button' onPress=succ label="Increment" />
    <button' onPress=pred label="Decrement" />
    <button' onPress=reset label="Reset" />
    <hello name=(Printf.sprintf "hello #%i" v) />
    <hello name=(Printf.sprintf "hello #%i" v) />
  </div>

let%component wait_and_print ~promise ?promise2 ~msg () =
  let () = React.use promise in
  let () = Option.map React.use promise2 |> Option.value ~default:() in
  <li>(React.string msg)</li>

let%component nav' () =
  <ul>
    <li><link' href="/" label="Main page" /></li>
    <li><link' href="/todo" label="TODOs" /></li>
    <li><link' href="/about" label="About" /></li>
  </ul>

type about_mode = About_light | About_dark [@@deriving to_json, of_json]

let%export_component about ~(mode : about_mode) ~(num : int) () =
  let%browser_only () =
    match mode with
    | About_dark -> Js.log "dark"
    | About_light -> Js.log "light"
  in
  <div><nav' /></div>

let%export_component app ~(title : string) ~(inner : React.element) () =
  let promise = Promise.sleep 1.0 in
  let promise2 = Promise.sleep 2.0 in
  let promise_inner = Promise.sleep 0.5 in
  let%browser_only () =
    React.useEffect1 (fun () -> Js.log "HELLO, I'M READY"; None) [||]
  in
  <div>
    <nav' />
    <React.Suspense>
      <hello name="world" /> <hello name="something else" />
    </React.Suspense>
    <h2>"Hello, " (React.string title) "!"</h2>
    <React.Suspense><counter init=42 title="Counter" /></React.Suspense>
    <div className="footer">inner inner</div>
    <ul>
      <React.Suspense>
        <React.Suspense>
          <wait_and_print promise=promise_inner msg="INNER SLEPT" />
        </React.Suspense>
        <React.Suspense>
          <wait_and_print promise=promise_inner msg="INNER SLEPT" />
        </React.Suspense>
        <wait_and_print promise promise2 msg="SLEPT" />
        <wait_and_print promise promise2 msg="SLEPT" />
      </React.Suspense>
    </ul>
  </div>

let%component todo_item ~on_completed ~todo () =
  let%browser_only onChange _ev =
    on_completed todo (not todo.Api.completed)
  in
  <li className="ma0 pa0 flex items-center">
    <label>
      <input className="scale3"
             type_="checkbox"
             checked=todo.Api.completed
             onChange />
      <span className="pl2">(React.string todo.Api.text)</span>
    </label>
  </li>

let%component todo_section ~title ~on_completed ~todos () =
  let todos =
    List.map
      (fun todo ->
        let key = Int.to_string todo.Api.id in
        <todo_item key on_completed todo />)
      todos
  in
  <div className="pb2">
    <h5 className="ma0 pv2">(React.string title)</h5>
    <ul className="ma0 pv0 ph2 flex flex-column g1">
      (React.array (Array.of_list todos))
    </ul>
  </div>

let%component add_todo_form ~on_create () =
  let value, set_value = React.useState (fun () -> "") in
  let input, set_input = ReactDOM.useDomRef () in
  let%browser_only onChange ev =
    let value = ReactEvent.Form.(target ev)##value in
    set_value (fun _ -> value)
  in
  let%browser_only create () =
    ignore
      Promise.(
        let* () = on_create value in
        set_value (fun _ -> "");
        let el = Js.Null_undefined.toOption input.current in
        let el = Option.bind el Webapi.Dom.HtmlElement.ofElement in
        Option.iter Webapi.Dom.HtmlElement.focus el;
        return ())
  in
  let%browser_only onKeyDown ev =
    match ReactEvent.Keyboard.key ev with
    | "Enter" -> create ()
    | _ -> ()
  in
  let%browser_only onClick _ev = create () in
  <div>
    <input ref=set_input onChange onKeyDown type_="text" value />
    <button onClick>"Add"</button>
  </div>

let%component todo_list' () =
  let todos, set_todos = React.useState Api.Todo.list in
  let todos = React.use (Remote.run_query todos) in
  let%browser_only refetch () =
    React.startTransition @@ fun () ->
    let%browser_only () = Remote.invalidate (Api.Todo.list ()) in
    set_todos (fun _ -> Api.Todo.list ())
  in
  let completed, to_be_done =
    List.partition_map
      (fun todo ->
        match todo.Api.completed with
        | true -> Left todo
        | false -> Right todo)
      todos
  in
  let%browser_only on_create text =
    Promise.(
      let* _new_todo = Remote.run_mutation @@ Api.Todo.create ~text () in
      refetch ();
      return ())
  in
  let%browser_only on_completed todo completed =
    ignore
      Promise.(
        let* _new_todo =
          Remote.run_mutation
          @@ Api.Todo.update ~completed ~id:todo.Api.id ()
        in
        refetch ();
        return ())
  in
  let%browser_only on_remove_completed _ev =
    ignore
      Promise.(
        let* _new_todo =
          Remote.run_mutation @@ Api.Todo.remove_completed ()
        in
        refetch ();
        return ())
  in
  <div>
    <add_todo_form on_create />
    <todo_section title="To be done" on_completed todos=to_be_done />
    <todo_section title="Completed" on_completed todos=completed />
    <button onClick=on_remove_completed>"Remove completed todos"</button>
  </div>

let%export_component todo_list () = <div><nav' /> <todo_list' /></div>

let%browser_only () =
  Js.log "this will execute only in browser on startup"
