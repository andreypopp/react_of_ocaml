open! ContainersLabels
open! Monomorphic
open Lwt.Infix

module UI = struct
  open React_server

  let%async_component card ~delay ~title ~children () =
    Lwt_unix.sleep delay >|= fun () ->
    <div className="ba pa2">
      <h3 className="ma0 pa0 pb2">(React.string title)</h3>
      <div className="pb2">children</div>
      <div className="f7 bt pa1">
        "I've been sleeping for "
        (React.stringf "%0.1f sec" delay)
        " before appearing"
      </div>
    </div>

  let%component page ~title ~children () =
    <html className="h-100">
      <head><title>(React.string title)</title></head>
      <body className="pa4 sans-serif dark-gray bg-washed-yellow h-100">
        <h1><span>(React.string title)</span></h1> <div>children</div>
      </body>
    </html>

  let app =
    <page title="React of OCaml">
      <div className="flex flex-column g2 measure-wide">
        <Example.app title="Hello from Client Component"
                     inner=(React.string
                              "As you can see, this one is SSR'ed as \
                               well.") />
        <card title="Initial Data" delay=0.>
          "This components loads some async data but will block the \
           shell until this data is ready."
        </card>
        <React.Suspense>
          <card title="Async Data" delay=1.>(React.string "HELLO")</card>
          <card title="Async Data" delay=1.>(React.string "HELLO")</card>
        </React.Suspense>
        <React.Suspense>
          <card title="Async Data" delay=2.>(React.string "OUTER")</card>
          <React.Suspense>
            <card title="Inner Async Data" delay=1.>
              (React.string "INNER")
            </card>
          </React.Suspense>
        </React.Suspense>
        <div>
          <h2>(React.string "Testing XSS")</h2>
          <ul>
            <li>"</script><script>console.log(1)</script>"</li>
            <li>"\u{2028}<script>console.log(1)</script>"</li>
          </ul>
        </div>
      </div>
    </page>

  let about =
    <page title="About">
      <div className="flex flex-column g2 measure-wide">
        <p>(React.string "Just an about page")</p>
        <Example.about num=1 mode=About_light />
      </div>
    </page>

  let todos =
    <page title="TODOs">
      <div className="flex flex-column g2 measure-wide">
        <Example.todo_list />
      </div>
    </page>
end

let links = [ "/static/bundle.css" ]
let scripts = [ "/static/bundle.js" ]

let render ?enable_ssr ui =
  React_dream.render ?enable_ssr ~enable_client_components:true ~links
    ~scripts ui

let handle =
  let f :
      type a.
      a Routing.t ->
      Dream.request ->
      a Ppx_deriving_router_runtime.return Lwt.t =
   fun route ->
    match route with
    | Routing.Home -> render UI.app
    | About -> render UI.about
    | Todo -> render UI.todos
    | No_ssr -> render ~enable_ssr:false UI.app
    | Api_hello route -> fun _req -> Data.Hello.handle route
    | Api_todo route -> fun _req -> Data.Todo.handle route
  in
  Routing.handle { f }

let () =
  let static =
    Static.Sites.static
    |> List.head_opt
    |> Option.get_exn_or "no /static dir found"
  in
  Dream.run ~interface:"127.0.0.1" ~port:8080
  @@ Dream.logger
  @@ Dream.router
       [
         Dream.get "static/**" (Dream.static static);
         Dream.any "**" handle;
       ]
